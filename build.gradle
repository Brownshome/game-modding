buildscript {
	dependencies {
		classpath 'brownshome:modding-plugin:alpha'
	}
}

plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'brownshome'
version = '0.1.0-SNAPSHOT'

java {
	modularity.inferModulePath = true
	withSourcesJar()
	withJavadocJar()
}

allprojects {
	repositories {
		maven {
			url = 'https://maven.pkg.github.com/Brownshome/logging'
			credentials {
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}

		jcenter()
	}
}

sourceSets {
	main {
		java.srcDirs = ['src/main']
		resources.srcDirs = ['res/main']
	}
	
	test {
		java.srcDirs = ['src/test']
		resources.srcDirs = ['res/test']
	}
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/game-modding')
			credentials {
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'modding'
			from components.java

			pom {
				name = 'Modding Library'
				description = 'A library for loading mod code at runtime with version resolution'
				url = 'https://github.com/Brownshome/game-modding'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/game-modding.git'
					url = 'https://github.com/Brownshome/game-modding'
				}
			}
		}
	}
}

dependencies {
	implementation "brownshome:logging:1.0.0";

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

	testImplementation 'org.yaml:snakeyaml:1.25'
}

test {
	useJUnitPlatform()
}