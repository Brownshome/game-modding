plugins {
	id 'java-library'
	id 'maven-publish'
}

sourceSets {
	main {
		java.srcDirs = ['src']
		resources.srcDirs = ['res']
	}
}

group = 'brownshome'
version = '0.1.0-SNAPSHOT'

java {
	modularity.inferModulePath = true
	withSourcesJar()
	withJavadocJar()
}

dependencies {
	implementation project(':annotation')
	implementation group: 'com.squareup', name: 'javapoet', version: '1.11.1'
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/Brownshome/game-modding')
			credentials {
				username = project.properties['gpr.usr']
				password = project.properties['gpr.key']
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'modding-annotation-processor'
			from components.java

			pom {
				name = 'Modding Annotation Processor'
				description = 'An annotation processor for mod generation'
				url = 'https://github.com/Brownshome/game-modding'
				developers {
					developer {
						id = 'Brownshome'
						name = 'James Brown'
						email = 'jamesphone180@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/Brownshome/game-modding.git'
					url = 'https://github.com/Brownshome/game-modding'
				}
			}
		}
	}
}